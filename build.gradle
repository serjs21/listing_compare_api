plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id "co.uzzu.dotenv.gradle" version "4.0.0"
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.7'
	id "com.diffplug.spotless" version "6.25.0"
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation "io.netty:netty-resolver-dns-native-macos:4.1.110.Final:osx-aarch_64"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
    toolVersion = '10.12.1'
    configFile = file("config/checkstyle/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.8.3'
}

spotless {
    java {
        googleJavaFormat("1.22.0")
        target("src/**/*.java")
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named("bootRun") {
	// Loads variables from .env (in project root) into the bootRun process env
	environment env.allVariables()
}